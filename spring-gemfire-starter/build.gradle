plugins {
  id 'java-library'
  id 'gemfire-repo-artifact-publishing'
  id 'spring-java-jar'
}

project.ext.set("pomProjectLongName","Spring Boot for VMware GemFire")
project.ext.set("pomProjectArtifactName","spring-boot-2.6-gemfire-${gemfireBaseVersion}")
project.ext.set("pomProjectDescription","Spring Boot For VMware GemFire")

description = "Spring Boot Starter for VMware GemFire"

dependencies {
  api "org.springframework.boot:spring-boot-starter:$springBootVersion"

  api project(':spring-gemfire')
  api project(':spring-gemfire-autoconfigure')

  runtimeOnly "org.springframework.shell:spring-shell:$springShellVersion"
}

task copySpringTestClasses(type: Exec) {
  dependsOn("cleanSpringTestClasses")
  workingDir "$rootDir/scripts"
  commandLine 'chmod', '+x', 'copySBDGTestResources.sh'
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    commandLine 'cmd', '/c', 'mycommand'
  } else {
    commandLine './copySBDGTestResources.sh', '-l', "${rootProject.rootDir}/tmp/spring-boot-geode", '-b',
        "${springBootDataGeodeVersion}", '-t', "${rootProject.rootDir}"
  }
}

task cleanSpringTestClasses(type: Exec) {
  commandLine 'rm', '-rf', "${rootProject.rootDir}/tmp"
}

test {
  dependsOn("copySpringTestClasses")
}

clean{
  dependsOn("cleanSpringTestClasses")
}

repositories {
  def additionalMavenRepoURLs = project.ext.get('additionalMavenRepoURLs')
  if (!additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
    additionalMavenRepoURLs.split(",").each {
      project.getRepositories()
              .maven(mavenRepository -> {
                mavenRepository.setUrl(uri(it.toString()));
              });
    }
  }
}
